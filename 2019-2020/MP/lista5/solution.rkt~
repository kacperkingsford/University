#lang racket

(provide conj conj-left conj-right conj?
         disj disj-left disj-right disj?
         neg neg-subf neg?
         var?)


(define (conj p q)
  (list 'conj p q))

(define (conj-left f)
  (second f))

(define (conj-right f)
  (third f))

(define (conj? t)
  (and (list? t)
        (= 3 (length t))
        (eq? 'conj (car t))))


(define (disj p q)
  (list 'disj p q))

(define (disj-left f)
  (second f))

(define (disj-right f)
  (third f))

(define (disj? t)
  (and (list? t)
        (= 3 (length t))
        (eq? 'disj (car t))))


(define (neg x)
  (list 'neg x))

(define (neg-subf x)
  (second x))

(define (neg? t)
  (and (list? t)
        (= 2 (length t))
        (eq? 'neg (car t))))


(define (var? t)
  (symbol? t))


(define (to-nnf f)
  (cond
   [(var? f)  (lit-pos f)]
   [(neg? f)  (to-nnf-neg (neg-subf f))]
   [(conj? f) (conj (to-nnf (conj-left f))
                    (to-nnf (conj-right f)))]
   [(disj? f) (disj (to-nnf (disj-left f))
                    (to-nnf (disj-right f)))]))

(define (to-nnf-neg f)
  (cond
   [(var? f)  (lit-neg f)]
   [(neg? f)  (to-nnf f)]
   [(conj? f) (disj (to-nnf-neg (conj-left f))
                    (to-nnf-neg (conj-right f)))]
   [(disj? f) (conj (to-nnf-neg (disj-left f))
                    (to-nnf-neg (disj-right f)))]))

(define (lit? f)
  (or (var? f)
      (and (neg? f)
           (var? (neg-subf f)))))

(define (lit-pos v)
  v)

(define (lit-neg v)
  (neg v))

(define (lit-var l)
  (if (var? l)
      l
      (neg-subf l)))

(define (lit-pos? l)
  (var? l))

(define (mk-cnf xss)
  (cons 'cnf xss))

(define (clause? f)
  (and (list? f)
       (andmap lit? f)))

(define (cnf? f)
  (and (pair? f)
       (eq? 'cnf (car f))
       (list? (cdr f))
       (andmap clause? (cdr f))))

(define (to-cnf f)
  (define (join xss yss)
    (apply append (map (lambda (xs) (map (lambda (ys) (append xs ys)) yss)) xss)))
  (define (go f)
    (cond
     [(lit? f)  (list (list f))]
     [(conj? f) (append (go (conj-left f))
                        (go (conj-right f)))]
     [(disj? f) (join (go (disj-left f))
                      (go (disj-right f)))]))
  (mk-cnf (go f)))

;(define (falsifiable-cnf? f)
  ;(error "TODO: Uzupelnij tutaj"))

