#lang racket
;; ---------------
;; Jezyk wejsciowy
;; ---------------

(struct pos (file line col)     #:transparent)
  
(struct const    (val)          #:transparent)
(struct binop    (op l r)       #:transparent)
(struct var-expr (id)           #:transparent)
(struct let-expr (loc id e1 e2) #:transparent)

(define (expr? e)
  (match e
    [(const n)      (number? n)]
    [(binop op l r) (and (symbol? op) (expr? l) (expr? r))]
    [(var-expr x)   (symbol? x)]
    [(let-expr loc x e1 e2)
     (and (pos? loc) (symbol? x) (expr? e1) (expr? e2))]
    [_ false]))

(define (make-pos s)
  (pos (syntax-source s)
       (syntax-line   s)
       (syntax-column s)))

(define (parse e)
  (let ([r (syntax-e e)])
    (cond
      [(number? r) (const r)]
      [(symbol? r) (var-expr r)]
      [(and (list? r) (= 3 (length r)))
       (match (syntax-e (car r))
         ['let (let* ([e-def (syntax-e (second r))]
                      [x     (syntax-e (first e-def))])
                 (let-expr (make-pos (first e-def))
                           (if (symbol? x) x (error "parse error!"))
                           (parse (second e-def))
                           (parse (third r))))]
         [op   (binop op (parse (second r)) (parse (third r)))])]
      [else (error "parse error!")])))

;; ---------------
;; Jezyk wyjsciowy
;; ---------------

(struct var-free  (id)     #:transparent)
(struct var-bound (pos id) #:transparent)

(define (expr-annot? e)
  (match e
    [(const n)         (number? n)]
    [(binop op l r)    (and (symbol? op) (expr-annot? l) (expr-annot? r))]
    [(var-free x)      (symbol? x)]
    [(var-bound loc x) (and (pos? loc) (symbol? x))]
    [(let-expr loc x e1 e2)
     (and (pos? loc) (symbol? x) (expr-annot? e1) (expr-annot? e2))]
    [_ false]))

(struct environ (xs))

; Kod z notatek, lekko zmieniony. 
(define env-empty (environ null))
(define (env-add x l env)
  (environ (cons (cons x l) (environ-xs env))))
(define (env-lookup x env) ; Zwraca var-free, jeśli zmienna nie jest związana, zaś var-bound jeśli jest związana.
  (define (assoc-lookup xs)
    (cond [(null? xs) (var-free x)]
          [(eq? x (car (car xs))) (var-bound (cdr (car xs)) x)]
          [else (assoc-lookup (cdr xs))]))
  (assoc-lookup (environ-xs env)))


(define (annotate-expression-env e env)
  (match e
  [(const n) e]
  [(var-expr x) (env-lookup x env)]
  [(binop op l r) (binop op (annotate-expression-env l env) (annotate-expression-env r env))]
  [(let-expr loc x e1 e2) (let-expr loc x (annotate-expression-env e1 env) (annotate-expression-env e2 (env-add x loc env)))]
  ))


(define (annotate-expression e)
  (annotate-expression-env e env-empty))

;Podczas sporządzania rozwiązania współpracowywałem z Adamem Jarząbkiem